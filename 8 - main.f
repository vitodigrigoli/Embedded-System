: SETUP_CHAR
	0 MYCHAR BOMB
	1 MYCHAR LOCK
	2 MYCHAR BELL
	3 MYCHAR CHECK
	4 MYCHAR HEART_EMPTY
	5 MYCHAR HEART
	6 MYCHAR SKULL
	CURSOR RETURN
;

: INIT_GPIO 
	RED LED OUTPUT
	BLUE LED OUTPUT
	GREEN LED OUTPUT
	YELLOW LED OUTPUT
	BUZZER OUTPUT

	RED BUTTON INPUT
	BLUE BUTTON INPUT
	GREEN BUTTON INPUT
	YELLOW BUTTON INPUT
	RESET BUTTON INPUT
	POWER BUTTON INPUT

	RESET BUTTON ACTIVATE
	RED BUTTON ACTIVATE
	BLUE BUTTON ACTIVATE
	GREEN BUTTON ACTIVATE
	YELLOW BUTTON ACTIVATE
;

: INIT_LCD
	I2C_SETUP
	1 SEC DELAY
	LCD_RESET
	1 SEC DELAY
	SETUP_CHAR
;


: INIT_DISPLAY
	A_SEGMENT OUTPUT
	B_SEGMENT OUTPUT
	C_SEGMENT OUTPUT
	D_SEGMENT OUTPUT
	E_SEGMENT OUTPUT
	F_SEGMENT OUTPUT
	G_SEGMENT OUTPUT
;

: MSG0 LCD CLEAR 'B 'O 'O 'T 'I 'N 'G '. '. '. ;
: MSG1 LCD CLEAR 'SPACE 'SPACE 'SPACE 'D 'E 'F 'U 'S 'E 'SPACE 'T 'H 'E DOWN CURSOR 'SPACE 'SPACE 'SPACE 'SPACE 0 >LCD 'SPACE 'B 'O 'M 'B 'SPACE 0 >LCD ;
: MSG2 LCD CLEAR 'SPACE 'SPACE 1 >LCD 'SPACE 'R 'E 'P 'L 'Y 'SPACE 'T 'H 'E DOWN CURSOR 'C 'O 'R 'R 'E 'C 'T 'SPACE 'S 'E 'Q 'U 'E 'N 'C 'E ;
: MSG3 LCD CLEAR 'SPACE 'SPACE 2 >LCD 'SPACE 'P 'R 'O 'T 'E 'C 'T 'I 'O 'N DOWN CURSOR 'SPACE 'SPACE 'M 'E 'C 'H 'A 'N 'I 'S 'M 'SPACE 'O 'N ;
: MSG4 LCD CLEAR 'SPACE 'SPACE 3 >LCD 'SPACE 'G 'R 'E 'A 'T 'SPACE 'J 'O 'B DOWN CURSOR 'SPACE 'SPACE 'SPACE 'SPACE 'B 'L 'A 'S 'T 'E 'R  ;
: MSG5 LCD CLEAR 'SPACE 'SPACE 'SPACE 5 >LCD 4 >LCD 'SPACE 'G 'A 'M 'E 'SPACE 4 >LCD 5 >LCD DOWN CURSOR 'SPACE 'SPACE 'SPACE 'SPACE 6 >LCD 'SPACE 'O 'V 'E 'R 'SPACE 6 >LCD ;


: BOOT 
	INIT_LCD
	MSG0
	CURSOR TURN_OFF
	INIT_GPIO 
	INIT_DISPLAY
	>OFF

	RESET_ALL! 
;


: CHECK_VALUE
	." CHECK_VALUE "
	= IF 1 ( GO TO STATE 1 ) ELSE
		2 ( GO TO STATE 2 ) THEN
;

: CHECK_PRESSED
	." CHECK_PRESSED "
	BEGIN 
		RED BUTTON PRESSED? 1 = IF RED LED BLINK RED BUTTON RESET! RED BUTTON 1 ELSE
		BLUE BUTTON PRESSED? 1 = IF BLUE LED BLINK BLUE BUTTON RESET! BLUE BUTTON 1 ELSE
		GREEN BUTTON PRESSED? 1 = IF GREEN LED BLINK GREEN BUTTON RESET! GREEN BUTTON 1 ELSE
		YELLOW BUTTON PRESSED? 1 = IF YELLOW LED BLINK YELLOW BUTTON RESET! YELLOW BUTTON 1 ELSE
		POWER BUTTON LEVEL? 0 = IF QUIT ELSE
		0 
		THEN THEN THEN THEN THEN
	UNTIL
;


: STATE_0
	." STATO 0 " CR

	MSG2

	RED LED BLINK
	BLUE LED BLINK
	YELLOW LED BLINK 
	RED LED BLINK  
	GREEN LED BLINK 

	GREEN BUTTON RED BUTTON YELLOW BUTTON BLUE BUTTON RED BUTTON
	1 	( GO TO STATE 1 )
;



: STATE_1
	." STATO 1 " CR
	DEPTH 0 > IF CHECK_PRESSED CHECK_VALUE  ELSE 3 ( GO TO STATE 3 ) THEN
;

: STATE_2
	." STATO 2 " CR
	STACK_CLEAR
	BUZZER BLINK
	0 ( GO TO STATE 0 )
;

: STATE_3 
	." STATO 3 " CR
	MSG3
	9 COUNTDOWN

	RESET BUTTON PRESSED? IF MSG4 RESET BUTTON RESET! ELSE MSG5 BUZZER ON 2 SEC DELAY BUZZER OFF THEN -1 ( EXIT FROM LOOP )

;


: MAIN
	BOOT

	MSG1
	0 ( INIT STATE )


	BEGIN POWER BUTTON LEVEL?  UNTIL

	BEGIN DUP -1 <> POWER BUTTON LEVEL? AND
	
	WHILE 

		CASE
			0 OF STATE_0 ENDOF
			1 OF STATE_1 ENDOF
			2 OF STATE_2 ENDOF
			3 OF STATE_3 ENDOF
		ENDCASE

	REPEAT RESET BUTTON RESET! STACK_CLEAR 3 SEC DELAY LCD CLEAR LCD SWITCH_OFF >OFF
;

